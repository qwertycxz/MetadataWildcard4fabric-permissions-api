plugins {
	id 'com.diffplug.spotless' version '7.1.0'
	id 'com.gladed.androidgitversion' version '0.4.14'
	id 'com.vanniktech.maven.publish' version '0.33.0'
	id 'fabric-loom' version '1.11-SNAPSHOT'
	id 'net.raphimc.class-token-replacer' version '1.1.7'
}

var DESCRIPTION = 'An addon for fabric-permissions-api that enables wildcard support in metadata.'
var DEVELOPER = 'qwerty吃小庄'
var GITHUB = 'https://github.com/qwertycxz'
var GROUP = 'top.qwertycxz'
var HOMEPAGE = 'https://qwertycxz.top'
var ID = 'metadatawildcard4fabric-permissions-api'
var ISSUE = 'https://github.com/qwertycxz/MetadataWildcard4fabric-permissions-api/issues'
var KO_FI = 'https://ko-fi.com/qwertycxz'
var LICENSE = 'The Apache License, Version 2.0'
var NAME = 'Metadata Wildcard for fabric-permissions-api'
var SCM = 'https://github.com/qwertycxz/MetadataWildcard4fabric-permissions-api'
var VERSION = androidGitVersion.name()

dependencies {
	mappings loom.officialMojangMappings()
	minecraft 'com.mojang:minecraft:1.21.7'
	modImplementation 'me.lucko:fabric-permissions-api:0.4.1'
	modImplementation 'net.fabricmc:fabric-loader:0.16.14'
	modImplementation fabricApi.module('fabric-api-base', '0.128.2+1.21.7')
}

loom {
	decompilers {
		cfr {
			memory = 1024
		}
	}
	runs {
		server {
			vmArgs '-Xmx1G'
		}
	}
	serverOnlyMinecraftJar()
}

mavenPublishing {
	coordinates GROUP, ID, VERSION
	pom {
		ciManagement {
			url = 'https://github.com/qwertycxz/MetadataWildcard4fabric-permissions-api/actions'
			system = 'GitHub Actions'
		}
		description = DESCRIPTION
		developers {
			developer {
				id = 'qwertycxz'
				name = DEVELOPER
				properties = [
					'GitHub': GITHUB,
					'Ko-fi': KO_FI,
				]
				timezone = 'Asia/Shanghai'
				url = HOMEPAGE
			}
		}
		inceptionYear = '2025'
		issueManagement {
			system = 'GitHub'
			url = ISSUE
		}
		licenses {
			license {
				distribution = 'repo'
				name = LICENSE
				url = 'https://www.apache.org/licenses/LICENSE-2.0'
			}
		}
		name = NAME
		scm {
			connection = 'scm:git:git://github.com/qwertycxz/MetadataWildcard4fabric-permissions-api'
			developerConnection = 'scm:git:ssh://github.com/qwertycxz/MetadataWildcard4fabric-permissions-api'
			url = SCM
		}
	}
	publishToMavenCentral()
	signAllPublications()
}

processResources {
	filesMatching('fabric.mod.json') {
		expand(
			description: DESCRIPTION,
			developer: DEVELOPER,
			github: GITHUB,
			homepage: HOMEPAGE,
			id: ID,
			issue: ISSUE,
			kofi: KO_FI,
			license: LICENSE,
			name: NAME,
			schema: '''schemaVersion": 1,
	"$schema''',
			scm: SCM,
			version: VERSION,
		)
	}
}

publishing {
	repositories {
		maven {
			name = 'githubPackages'
			url = 'https://maven.pkg.github.com/qwertycxz/MetadataWildcard4fabric-permissions-api'
			credentials PasswordCredentials
		}
	}
}

sourceSets {
	main {
		classTokenReplacer {
			property '$id', ID
			replaceInPlace = true
		}
	}
}

spotless {
	json {
		jackson() feature('ORDER_MAP_ENTRIES_BY_KEYS', true) jsonFeature('ESCAPE_FORWARD_SLASHES', false) jsonFeature('STRICT_DUPLICATE_DETECTION', true)
		eclipseWtp 'json'
		endWithNewline()
		setLineEndings 'UNIX'
		target 'src/**/*.json'
	}
}

tasks.withType JavaCompile configureEach {
	options.deprecation = true
	options.encoding = 'UTF-8'
	options.listFiles = true
	options.release = 15
}
